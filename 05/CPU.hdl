// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    /*WriteM*/
    And(a=instruction[15], b=instruction[3], out=writeM);

    /*A Register*/
    Mux16(a=instruction, b=ALUout, sel=instruction[15], out=q1);
    Not(in=instruction[15], out=w1);
    Or(a=w1, b=instruction[5], out=Aload);
    ARegister(in=q1, load=Aload, out=Aout, out[0..14]=addressM);
    
    /*D Register*/
    And(a=instruction[15], b=instruction[4], out=Dload);
    DRegister(in=ALUout, load=Dload, out=ALUx);

    /*ALU*/
    Mux16(a=Aout, b=inM, sel=instruction[12], out=ALUy);
    ALU(x=ALUx, y=ALUy, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], 
        zr=zr, ng=ng, out=ALUout, out=outM);
    
    /*PC*/
    Not(in=zr, out=NOTzero);
    Not(in=ng, out=NOTneg);
    And(a=NOTzero, b=NOTneg, out=GTzero);
    And(a=instruction[1], b=instruction[0], out=j2j3);
    And(a=instruction[2], b=instruction[0], out=j1j3);
    And(a=instruction[2], b=instruction[1], out=j1j2);
    Or(a=zr, b=ng, out=ZEROorNEG);

    And(a=instruction[0], b=GTzero, out=Or1);
    And(a=instruction[1], b=zr, out=Or2);
    And(a=j2j3, b=NOTneg, out=Or3);
    And(a=instruction[2], b=ng, out=Or4);
    And(a=j1j3, b=NOTzero, out=Or5);
    And(a=j1j2, b=ZEROorNEG, out=Or6);
    And(a=j1j2, b=instruction[0], out=Or7);

    Or8Way(in[0]=false, in[1]=Or1, in[2]=Or2, in[3]=Or3, in[4]=Or4, in[5]=Or5, in[6]=Or6, in[7]=Or7, out=Or8WayOut);
    And(a=Or8WayOut, b=instruction[15], out=PCload);
    PC(in=Aout, inc=true, load=PCload, reset=reset, out[0..14]=pc);
}
